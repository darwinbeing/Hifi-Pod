#
# qmake configuration for building Qt5 with arm-linux-g++ - DM3730
#

MAKEFILE_GENERATOR      = UNIX
TARGET_PLATFORM         = unix
TEMPLATE                = app
CONFIG                  += qt warn_on release incremental link_prl gdb_dwarf_index
QT                      += core gui
QMAKE_INCREMENTAL_STYLE = sublib


include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)



#User specific Path - To Be edited by User
QT_INSTALL_DIR          = /home/prabindh/work1/qt5/qt5
SGX_SDK_ROOT	        = /home/prabindh/Graphics_SDK_4_05_00_03
TSLIB_INCDIR            = 
TSLIB_LIBDIR            = 

#aragogcc 4.3
#DVSDK_INSTALL_DIR = /home/prabindh/3730_dvsdk_targetfs
#gcc4.5
DVSDK_INSTALL_DIR = /home/prabindh/work1/ARAGO_ROOTFS_4.5

ROOTFS_LIBDIR = $$DVSDK_INSTALL_DIR/usr/lib
GLIB_LIBDIR = $$DVSDK_INSTALL_DIR/opt/gstreamer/lib/

GLIB_INCDIR            = $$DVSDK_INSTALL_DIR/opt/gstreamer/include/glib-2.0
GLIB_CONFIGINCLUDEDIR	= $$DVSDK_INSTALL_DIR/opt/gstreamer/lib/glib-2.0/include
GST_INCLUDEDIR	= $$DVSDK_INSTALL_DIR/opt/gstreamer/include/gstreamer-0.10
LIBXML_INCLUDEDIR = $$DVSDK_INSTALL_DIR/usr/include/libxml2
ALSA_INCLUDEDIR = $$DVSDK_INSTALL_DIR/usr/include

#Needed for Qt5 Webkit build
ICU_INCLUDEDIR = /home/prabindh/work1/arm-icu-install/usr/local/include
ICU_LIBDIR = /home/prabindh/work1/arm-icu-install/usr/local/lib

# modifications to g++.conf
QMAKE_CFLAGS	+= -O3 -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp
QMAKE_CXXFLAGS	+= -O3 -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp

QMAKE_CC                = arm-arago-linux-gnueabi-gcc
QMAKE_CXX               = arm-arago-linux-gnueabi-g++
QMAKE_LINK              = arm-arago-linux-gnueabi-g++
QMAKE_LINK_SHLIB        = arm-arago-linux-gnueabi-g++

# modifications to linux.conf
QMAKE_AR                = arm-arago-linux-gnueabi-ar cqs
QMAKE_OBJCOPY           = arm-arago-linux-gnueabi-objcopy
QMAKE_STRIP             = arm-arago-linux-gnueabi-strip

#Do not edit beyond this

QMAKE_INCDIR_OPENGL_ES2 = $$SGX_SDK_ROOT/include/OGLES2
QMAKE_LIBDIR_OPENGL_ES2 = $$SGX_SDK_ROOT/gfx_dbg_es5.x
QMAKE_LIBS_OPENGL_ES2   = -lEGL -lGLESv2 -lGLES_CM -lIMGegl -lsrv_um -lusc

QMAKE_INCDIR_OPENGL     += $$QMAKE_INCDIR_OPENGL_ES2
QMAKE_LIBDIR_OPENGL     = $$QMAKE_LIBDIR_OPENGL_ES2
QMAKE_LIBDIR_OPENGL_QT  = $$QMAKE_LIBDIR_OPENGL_ES2
QMAKE_LIBS_OPENGL_ES1   = -lEGL -lGLES_CM -lIMGegl -lsrv_um -lusc

QMAKE_INCDIR_OPENVG     = $$QMAKE_INCDIR_OPENGL_ES2
QMAKE_LIBDIR_OPENVG     = $$QMAKE_LIBDIR_OPENGL_ES2
QMAKE_LIBS_OPENVG       = -lEGL -lGLESv2 -lGLES_CM -lIMGegl -lsrv_um -lOpenVG -lOpenVGU

QMAKE_INCDIR_EGL        = $$QMAKE_INCDIR_OPENGL_ES2
QMAKE_INCDIR_EGL        += $$QT_INSTALL_DIR/src/3rdparty/powervr/wsegl2
QMAKE_INCDIR_POWERVR    = $$QT_INSTALL_DIR/src/3rdparty/powervr/wsegl2
QMAKE_LIBDIR_EGL        = $$QMAKE_LIBDIR_OPENGL_ES2
QMAKE_LIBS_EGL          = -lEGL -lIMGegl -lsrv_um -lGLESv2 -lGLES_CM -lusc

QMAKE_INCDIR += $$TSLIB_INCDIR 
QMAKE_INCDIR += $$GLIB_INCDIR 
QMAKE_INCDIR += $$GLIB_CONFIGINCLUDEDIR
QMAKE_INCDIR += $$GST_INCLUDEDIR
QMAKE_INCDIR += $$LIBXML_INCLUDEDIR
QMAKE_INCDIR += $$ICU_INCLUDEDIR
QMAKE_INCDIR += $$ALSA_INCLUDEDIR

QMAKE_INCDIR += $$QMAKE_INCDIR_OPENGL_ES2
QMAKE_LIBDIR += $$QMAKE_LIBDIR_OPENGL_ES2 
QMAKE_LIBDIR += $$TSLIB_LIBDIR
QMAKE_LIBDIR += $$ICU_LIBDIR

QMAKE_LIBDIR += $$ROOTFS_LIBDIR

QMAKE_LIBDIR += $$GLIB_LIBDIR

#QMAKE_LIBS += $$QMAKE_LIBS_OPENGL_ES2 -lts -lglib -lgthread-2.0 -lgstreamer-0.10
QMAKE_LIBS += $$QMAKE_LIBS_OPENGL_ES2 -lglib-2.0 -lgthread-2.0 -lgstreamer-0.10 -lgobject-2.0 -lgmodule-2.0

load(qt_config)
